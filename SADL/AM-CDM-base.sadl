uri "http://kdl.ge.com/AM-CDM-base" alias AM-CDM-base version "$Revision:$ Last modified on   $Date:$".

Document (note "File of any type.") is a class,
	described by documentName with a single value of type string,
	described by documentLocation with a single value of type string,
	described by documentClassificationLevel with a single value of type string,
	described by documentClassificationIPClass with a single value of type string,
	described by documentURI with a single value of type string.

Organization (note "An organization of various types.") is a class,
	described by organizationID (note "The unique identifier of the organization.") with a single value of type string,
	described by organizationName (note "The name of an organization.") with a single value of type string,
	described by organizationType (note "The type or roles of an organization, whether it is a manufacturer, vendor, supplier, or contractor.  One organization may have many types.") with values of type OrganizationType,
	described by organizationQualification (note "Details of the organization's qualifications and certifications.") with values of type OrganizationQualification,
	described by organizationAddress (note "The address of the organization.") with a single value of type Address,
	described by organizationPhoneNumber (note "A phone number for the organization.") with a single value of type PhoneNumber,
	described by organizationPointOfContact (note "Identification of the point of contact.") with a single value of type Person.

OrganizationType is a class, must be one of {eMaterialManufacturer, MaterialSupplier, PartDesigner, PartManufacturer, MachineManufacturer, SystemIntegrator, PartTestr}.

//Orientation - look for Pink comments in Google Docs file!

Person (note "Individuals and their primary credentials and contact information.") is a class,
	described by personID (note "Unique identifier of the person.") with a single value of type string,
	described by personFirstName (note "First name of individual performing task.") with a single value of type string,
	described by personLastName (note "Last name of individual performing task.") with a single value of type string,
	//described by personRole (note "Role of individual performing task.") with a single value of type string,
	described by personEmail (note "The email for the individual.") with a single value of type string,
	described by personPhoneNumber (note "The phone number for the individual.") with values of type PhoneNumber,
	described by personQualification (note "Qualifications of person.") with values of type PersonQualification,
	described by personOrganization (note "Links person to an organization") with a single value of type Organization.

OrganizationQualification (note "Details of qualification/certification for an organization.") is a type of Qualification.

PersonQualification (note "Details of qualification/certification for a person.") is a type of Qualification,
	described by personQualificationNumberOfHours (note "The number of hours that this person has completed on this equipment/software/system.") with a single value of type int.

Qualification (note "Details of qualification/certification (machine specific operator certificate, UL, ASTM).") is a class,
	described by qualificationType (note "The type of qualification/certification.") with a single value of type string,
	described by qualificationLevel (note "The level of qualification/certification.") with a single value of type string,
	described by qualifyingOrganization (note "The organization from which the qualification or certification originates.") with a single value of type Organization.

Address is a class,
	described by addressLine1 with a single value of type string,
	described by addressLine2 with a single value of type string,
	described by city with a single value of type string,
	described by stateProvince with a single value of type string,
	described by postalCode with a single value of type string,
	described by country with a single value of type Country.

PhoneNumber is a class,
	described by phoneCountry with a single value of type Country,
	described by phoneNumber with a single value of type string.

Country is a class,
	described by countryName with a single value of type string,
	described by countryAlpha2Code with a single value of type string,
	described by countryAlpha3Code with a single value of type string,
	described by countryCallingCode with a single value of type int.

CartesianCoordinate is a class,
	described by xCoordinate with a single value of type Measurement,
	described by yCoordinate with a single value of type Measurement,
	described by zCoordinate with a single value of type Measurement.

Orientation is a class.
//RotationThenTranslationOrientation?
//TranslationThenRotationOrientation?
//Can we replicate how Magics defines orientation?

Measurement is a class,
	described by measurementValue with a single value of type double,
	described by measurementUnit with a single value of type string,
	described by rawDataValues with values of type RawDataValue,
	described by rawDataAggregationTechnique with a single value of type string.
	// need to add measure(s) of uncertainty -- standard deviation, more?

RawDataValue is a class,
	//described by rawDataTag with a single value of type string,
	described by rawDataTime with a single value of type dateTime,
	described by rawDataValue with a single value of type Measurement,
	described by rawDataQuality with a single value of type string.

DatasetMetadata is a class,
	described by datasetName (note "Name of the dataset.") with a single value of type string,
	described by datasetID (note "Unique identifier of the dataset.") with a single value of type string,
	described by datasetSubject (note "Classification, keyword, or phrase that describes the topic of the dataset at the endpoint of the URI. This is a refinement of the Dublin Core definition that states this is the topic of the resource and the plurality of keywords, etc. It should be noted that the recommended best practice is to use a controlled vocabulary (per Dublin Core).") with values of type string,
	described by datasetType (note "Media type, formerly known as MIME Type, a two-part identifier for file formats and format contents transmitted on the Internet. The Internet Assigned Numbers Authority (IANA) is the official authority for the standardization and publication of these classifications.") with a single value of type string,
	described by datasetURI (note "URI location where a dataset can be found.") with a single value of type string,
	described by datesetFormat (note "Formal description language that describes the format or layout of the file, such as CSV with Headers, Protobuf, fixed position, JSON Schema, XML, Cobol Copybook, X12 and so forth.") with values of type string,
	described by datasetSchemaURI (note "Location via a URI pointer to the specific Content Format Specification that describes how to parse the content in a repository; i.e., defines the schema of the content in the repository.") with a single value of type string,
	described by datasetCreator (note "Creator of dataset and/or the metadata of the dataset.") with values of type Person,
	described by datasetCreationDateTime (note "Point of time associated with creation of the dataset. May be used at any level of granularity. Recommended best practice is to use an encoding scheme, such as the W3CDTF profile of ISO 8601 [W3CDTF].") with a single value of type dateTime,
	described by datasetSize (note "The size of the dataset.") with a single value of type Measurement,
	described by datesetType (note "Dataset type, raw data or synthesized data.") with values of type string,
	described by datasetQuantityType (note "Type of Measurement(s) that would appear in a dataset; e.g., pH, Moisture, Temperature, requiring an industry group to define the enumerated list/controlled vocabulary for the specific business context.") with values of type string,
	described by datasetKeyTags (note "Keywords and tags describing the data to facilitate search and findability.  Examples include 'meltpool temperature', 'meltpool size', and 'layer-wise image.'") with values of type string,
	described by datasetAccessControl (note "How to get access to the dataset. Control Code Type is used to communicate 'Restricted', 'Consent', and 'Public'.") with a single value of type DatasetAccessControl.

DatasetAccessControl is a class, must be one of {Restricted, Public}.
