uri "http://kdl.ge.com/AM-CDM-material" alias AM-CDM-material version "$Revision:$ Last modified on   $Date:$".

import "http://kdl.ge.com/AM-CDM-base".

PowderLot is a type of Material,
	described by virginOrUsed with a single value of type string,
	described by reuseNumber with a single value of type int.

Material (note "Specific instance of a material utilized in an AM process") is a class,
	described by materialID (note "Unique identifier for specific material instance") with a single value of type string,
	described by materialType (note "Generic material this specific instance is an instantiation of") with a single value of type MaterialType,
	described by materialGrade (note "Standard grade the material product conforms to") with a single value of type string,
	described by actualChemistry (note "Chemistry characterization for a material stock") with a single value of type Chemistry,
	described by materialStock (note "Stock information about the material from vendor/manufacturer") with a single value of type MaterialStock,
	described by measuredMaterialProperties with values of type IntrinsicMaterialProperties,
// how does the specificMaterialProperties align or misalign with the MaterialSpecification class?
	described by specifiedMaterialProperties (note "Vendor-provided extrinsic or intrinsic material properties") with values of type MaterialProperties,
	described by predictedMaterialProperties (note "Capture ICME simulated material properties") with values of type MaterialProperties,
	described by parents (note "Parent or parents of material if it is blended") with values of type ParentMaterial.

MaterialStock is a class,
	described by materialStockForm (note "Form of a material stock") with a single value of type MaterialStockForm,
	described by materialStockOwner (note "Owner/organization of a material stock") with a single value of type Organization,
	described by materialStockLocation (note "Location of a material stock") with a single value of type Address,
	// should this be the actual storage environment -- CDD label suggests otherwise, but then redundant with materialStorageRequirements field
	described by materialStockStorageEnvironment (note "Environment specified by the vendor of a material stock - temp, humidity, light") with a single value of type Document,

	described by materialPackagingRequirements (note "Packaging standard operating procedure and environmental tolerances required by a material product") with a single value of type Document,
	described by materialStorageRequirements (note "Environmental tolerances, handling standard operating procedure required by a material product") with a single value of type Document,
	described by materialProductSpecification (note "Material data sheet provided by a material supplier or manufacturer") with a single value of type Document,

	described by materialPurchaseOrderNumber (note "Reference to the internal purchase order") with a single value of type string,
	described by materialSupplier (note "Name of the material vendor") with a single value of type Organization, // not specified if material is a blend from multiple lots
	described by materialManufacturer (note "Name of the material manufacturer") with a single value of type Organization, // not specified if material is a blend from multiple lots
	described by vendorManufacturingLot (note "Unique manufacturing lot number assigned by the manufacturer to the material stock") with a single value of type string,
	described by materialStockQuantityPurchased (note "Purchased quantity of a material stock") with a single value of type Measurement,

	described by materialStockCertificate (note "Link to a material stock certificate") with a single value of type Document,
	described by materialStockCertificationDate (note "Purchase/certification date of a material stock") with a single value of type dateTime.

MaterialStockForm (note "Form of a material stock") is a class, must be one of {Powder, Wire, BulkSolid}.

// procedures for mixing/blending - part of Material or ParentMaterial?
ParentMaterial is a class,
	described by parentMaterial with a single value of type Material,
	described by parentMaterialPercent with a single value of type double.

MaterialType (note "Generic material type") is a class,
	described by materialName (note "Name of a material, e.g., Inconel 625") with a single value of type string,
	described by materialTypeDescription (note "Description of generic material") with a single value of type string,
	described by baseElement (note "Base element of material from periodic table") with a single value of type PeriodicElement,
	described by materialSpecification (note "Ideal chemistry specification of generic material") with a single value of type MaterialSpecification.

MaterialSpecification is a class,
	described by specChemistry with a single value of type Chemistry,
	described by predictedProperties with values of type MaterialProperties.

// ASM - reports "Standard Values" of materials
// also want to represent uncertainty in value(s)

MaterialProperties is a class,
	described by derivedBy with a single value of type PropertyDerivedBy,
	described by propertyReferenceStandard (note "ASTM or other standard measurement conforms to") with a single value of type Document,
	described by propertyDataSheet (note "Reference document used to provide material properties") with a single value of type Document.

PropertyDerivedBy is a class, must be one of {Measured, Predicted}.

IntrinsicMaterialProperties (note "Properties that are independent of the processing/part characteristics, such as part dimensions") is a type of MaterialProperties.

ExtrinsicMaterialProperties (note "Properties that are dependent upon the processing/part characteristics, such as part dimensions") is a type of MaterialProperties.

ThermodynamicProperties is a type of IntrinsicMaterialProperties,
	described by electricalConductivity with a single value of type Measurement,
	described by thermalExpansionCoef with a single value of type Measurement,
	described by thermalConductivity with a single value of type Measurement,
	described by heatCapacity with a single value of type Measurement,
	described by meltingTemperature with a single value of type Measurement.

ParticleSizeDistribution is a type of IntrinsicMaterialProperties,
	described by psdPrincipleOfMeasurement with a single value of type string,
	described by psdParameterBasis with a single value of type string,
	described by psdDispersionMedium with a single value of type string,
	described by psdDispersionMethod with a single value of type string,
	described by psdDispersionType with a single value of type string,
	described by psdConcentration with a single value of type Measurement,
	described by psdIndexOfRefractions with a single value of type double,
	described by psdScatteringModel with a single value of type string,
	// I'm not understanding what the 'psd_diam' and 'psd_perc' variables are supposed to represent
	// are these two arrays supposed to correspond?
	//described by psdDiameter with a single value of type string,
	//described by psdPercent with a single value of type string,
	// what does 'dv' stand for in 'dv10'?
	described by psd_dv10 with a single value of type Measurement,
	described by psd_dv50 with a single value of type Measurement,
	described by psd_dv90 with a single value of type Measurement,
	described by psdMean with a single value of type Measurement,
	described by psdMode with a single value of type Measurement,
	described by psdStandardDeviation with a single value of type Measurement,
	described by psdRange with a single value of type Measurement,
	described by psdSkewness with a single value of type Measurement,
	described by psdKurtosis with a single value of type Measurement.

TensileProperties is a type of ExtrinsicMaterialProperties,
	described by tensileTestStrainRate with a single value of type string,
	described by tensileTestDisplacementRate with a single value of type string,
	described by tensileTestControlMode with a single value of type string,
	described by initialStrainRate with a single value of type Measurement,
	described by initialLoadingRate with a single value of type Measurement,
	described by finalStrainRate with a single value of type Measurement,
	described by finalLoadingRate with a single value of type Measurement,
	described by yieldStrengthZeroPoint2Percent with a single value of type Measurement,
	described by yieldStrengthZeroPointZero2Percent with a single value of type Measurement,
	described by ultimateTensileStrength with a single value of type Measurement,
	described by elongation with a single value of type Measurement,
	described by percentReductionInArea with a single value of type Measurement,
	described by youngsModulus with a single value of type Measurement,
	described by maximumLoad with a single value of type Measurement.

CreepProperties is a type of ExtrinsicMaterialProperties,
	described by creepPanLoad with a single value of type Measurement,
	described by creepLeverArmRatio with a single value of type string,
	described by creepTestLoad with a single value of type string,
	described by creepTestStress with a single value of type string,
	described by creepTestDuration with a single value of type Measurement,
	described by elongationAtRuptureCreep with a single value of type Measurement.

ChemicalProperties is a type of ExtrinsicMaterialProperties,
	described by oxidationResistance with a single value of type Measurement.





// describes material characteristics from vendor
MaterialDefinition is a class.
// MaterialDataSheet ???

// MatOnto - from Clare Paul - uses ASM definitions!

Chemistry is a class,
	described by chemistryMeasurementMethod with a single value of type string,
	described by chemistryPercentUnit with a single value of type ChemistryPercentUnit,
	described by elements with values of type ChemistryElement.

ChemistryPercentUnit is a class, must be one of {WeightPercent, AtomicPercent}.

ChemistryElement is a class,
	described by chemistryElement with a single value of type PeriodicElement,
	described by minPercent with a single value of type Measurement,
	described by maxPercent with a single value of type Measurement,
	described by avgPercent with a single value of type Measurement.
/*
ChemistryElement is a class,
	described by chemistryElement with a single value of type PeriodicElement,
	// could do the below or have an attribute that defines whether the percent is weight or atomic
	described by minWeightPercent with a single value of type Measurement,
	described by maxWeightPercent with a single value of type Measurement,
	described by avgWeightPercent with a single value of type Measurement,
	described by minAtomicPercent with a single value of type Measurement,
	described by maxAtomicPercent with a single value of type Measurement,
	described by avgAtomicPercent with a single value of type Measurement.
*/

PeriodicElement is a class,
	described by elementName with a single value of type string,
	described by elementSymbol with a single value of type string,
	described by atomicNumber with a single value of type int,
	described by atomicMass with a single value of type Measurement,
	described by elementMeltingPoint with a single value of type Measurement.
